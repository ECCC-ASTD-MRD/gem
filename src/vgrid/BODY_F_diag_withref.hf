      use vgrid_utils, only: get_allocate
      ! Given referent, compute physical levelling information from the vertical description
      type(vgrid_descriptor), intent(in) :: self                  !Vertical descriptor instance
      integer, target, dimension(:), intent(in) :: ip1_list               !Key of prototype field
      real(kind=REAL_KIND), dimension(:,:,:), pointer :: levels                   !Physical level values
      real(kind=REAL_KIND), dimension(:,:), optional, target, intent(in) :: sfc_field     !Surface field reference for coordinate [none]
      real(kind=REAL_KIND), dimension(:,:), optional, target, intent(in) :: sfc_field_ls  !Surface field large scale reference for coordinate [none]
      logical, optional, intent(in) :: in_log                     !Compute levels in ln() [.false.]
      logical, optional, intent(in) :: dpidpis                    !Compute partial derivative of hydrostatic pressure (pi) with
      !   respect to surface hydrostatic pressure(pis) [.false.]
      
      ! Local variables
      integer istat,ni,nj,nk,error
      real(kind=REAL_KIND), dimension(:,:), pointer :: my_sfc_field, my_sfc_field_ls
      real(kind=REAL_KIND), dimension(:,:,:), pointer :: my_levels 
      type (c_ptr) :: ip1_list_CP ,levels_CP ,sfc_field_CP, sfc_field_ls_CP
      integer :: in_log_int, dpidpis_int
      logical :: my_dpidpis, alloc_my_sfc_field_L, alloc_my_sfc_field_ls_L, alloc_my_levels_L

      ! Set error status
      status = VGD_ERROR
      
      if(.not.is_valid(self,'SELF'))then
         write(for_msg,*) 'vgrid structure is not valid in diag_withref'//PROC_SUFF
         call msg(MSG_ERROR,VGD_PRFX//for_msg)       
         return
      endif
      
      ! Set default values      
      in_log_int = 0
      if (present(in_log))then
         if(in_log)then
            in_log_int = 1
         else
            in_log_int = 0
         endif
      endif
      my_dpidpis=.false.
      if(present(dpidpis))my_dpidpis=dpidpis
      dpidpis_int = 0
      if(my_dpidpis)then
         dpidpis_int = 1
      else
         dpidpis_int = 0
      endif
      
      alloc_my_sfc_field_L = .false.; alloc_my_sfc_field_ls_L = .false.; alloc_my_levels_L = .false.

      if (present(sfc_field)) then

         ni = size(sfc_field,dim=1); nj = size(sfc_field,dim=2); nk = size(ip1_list)

#ifdef WITH_intel
!        is_contiguous is not Fortran 2008 standard but Intel
         if(is_contiguous(sfc_field))then
            my_sfc_field => sfc_field
         else
#endif

!        Copy in and out 
            alloc_my_sfc_field_L = .true.
            allocate(my_sfc_field(ni,nj),stat=error)
            if (error /= 0) then
               write(for_msg,*) 'cannot allocate space for my_sfc_field in diag_withref'//PROC_SUFF
               call msg(MSG_ERROR,VGD_PRFX//for_msg)
               return
            endif
            my_sfc_field(1:ni,1:nj) = sfc_field(1:ni,1:nj)

#ifdef WITH_intel
         endif 
#endif

      else
         if (is_valid(self,"ref_name_valid")) then
            write(for_msg,*) 'reference field must be provided to diag_withref'//PROC_SUFF
            call msg(MSG_ERROR,VGD_PRFX//for_msg)
            return
         else
            ni = 1; nj = 1; nk = size(ip1_list)
         endif
      endif
      
      if (present(sfc_field_ls)) then
         if(  ni /= size(sfc_field_ls,dim=1) .or. &
              nj /= size(sfc_field_ls,dim=2) )then
            write(for_msg,*) 'reference large scale field is not of same size has reference field'
            call msg(MSG_ERROR,VGD_PRFX//for_msg)
            return
         endif

#ifdef WITH_intel
!        is_contiguous is not Fortran 2008 standard but Intel
         if(is_contiguous(sfc_field_ls))then
            my_sfc_field_ls => sfc_field_ls
         else
#endif
!        Copy in and out instead
            alloc_my_sfc_field_ls_L = .true.
            allocate(my_sfc_field_ls(ni,nj),stat=error)
            if (error /= 0) then
               write(for_msg,*) 'cannot allocate space for my_sfc_field_ls in diag_withref'//PROC_SUFF
               call msg(MSG_ERROR,VGD_PRFX//for_msg)
               return
            endif
            my_sfc_field_ls(1:ni,1:nj) = sfc_field_ls(1:ni,1:nj)

#ifdef WITH_intel
         endif
#endif
      else
         if (is_valid(self,"ref_namel_valid") .and. .not. my_dpidpis) then
            write(for_msg,*) 'reference large scale field must be provided to diag_withref'//PROC_SUFF
            call msg(MSG_ERROR,VGD_PRFX//for_msg)
            return
         endif
      endif
      if (associated(levels)) then
         if (size(levels,dim=1) /= ni .or. size(levels,dim=2) /= nj .or. size(levels,dim=3) /= nk) then
            if(ALLOW_RESHAPE)then
               write(for_msg,*) 'Levels array size error - will be reallocated'
               call msg(MSG_WARNING,VGD_PRFX//for_msg)
               deallocate(levels)
            else
               write(for_msg,*) 'Levels array size error - will not reallocate since ALLOW_RESHAPE is set to false'
               call msg(MSG_ERROR,VGD_PRFX//for_msg)
               return
            endif
         endif
      endif
      if(.not. associated(levels) )then
         allocate(levels(ni,nj,nk),stat=error)
         if (error /= 0) then
            write(for_msg,*) 'cannot allocate space for levels in diag_withref'//PROC_SUFF
            call msg(MSG_ERROR,VGD_PRFX//for_msg)
            return
         endif
      endif

#ifdef WITH_intel
!        is_contiguous is not Fortran 2008 standard but Intel
      if(is_contiguous(levels))then
         my_levels => levels
      else
#endif
!        Copy in and out instead
         alloc_my_levels_L = .true.
         allocate(my_levels(ni,nj,nk),stat=error)
         if (error /= 0) then
            write(for_msg,*) 'cannot allocate space for my_levels in diag_withref'//PROC_SUFF
            call msg(MSG_ERROR,VGD_PRFX//for_msg)
            return
         endif

#ifdef WITH_intel
      endif
#endif
      
      ip1_list_CP  = c_loc(ip1_list)
      levels_CP    = c_loc(my_levels(1,1,1))
      sfc_field_CP = C_NULL_PTR
      if (present(sfc_field)) sfc_field_CP = c_loc(my_sfc_field(1,1))
      sfc_field_ls_CP = C_NULL_PTR
      if (present(sfc_field_ls)) sfc_field_ls_CP = c_loc(my_sfc_field_ls(1,1))
#if defined(REAL_8)
      istat = f_diag_withref_8(self%cptr,ni,nj,nk,ip1_list_CP,levels_CP,sfc_field_CP,sfc_field_ls_CP,in_log_int,dpidpis_int)
#else
      istat = f_diag_withref(self%cptr,ni,nj,nk,ip1_list_CP,levels_CP,sfc_field_CP,sfc_field_ls_CP,in_log_int,dpidpis_int)
#endif
      if (istat /= VGD_OK) then
         if(my_dpidpis)then
            write(for_msg,*) 'error computing dpidpis in diag_withref'//PROC_SUFF
            call msg(MSG_ERROR,VGD_PRFX//for_msg)
         else
            write(for_msg,*) 'error computing pressure in diag_withref'//PROC_SUFF
            call msg(MSG_ERROR,VGD_PRFX//for_msg)
         endif
         return
      endif
      if(alloc_my_sfc_field_L)    deallocate(my_sfc_field)
      if(alloc_my_sfc_field_ls_L) deallocate(my_sfc_field_ls)
      if(alloc_my_levels_L)then
         levels(1:ni,1:nj,1:nk) = my_levels(1:ni,1:nj,1:nk)
         deallocate(my_levels)
      end if
      
      ! Set status and return
      status = VGD_OK
      return
