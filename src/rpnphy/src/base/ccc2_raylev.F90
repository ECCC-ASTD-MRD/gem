
subroutine ccc2_raylev2(taur, ig, dp, rmu3, il1, il2, ilg, lay)
   implicit none
!!!#include <arch_specific.hf>

   integer ilg, lay, il1, il2, ig
   real  taur(ilg,lay), dp(ilg,lay), rmu3(ilg)

   !@Authors
   !        J. Li, M. Lazare, CCCMA, rt code for gcm4
   !        (Ref: J. Li, H. W. Barker, 2005:
   !        JAS Vol. 62, no. 2, pp. 286\226309)
   !        P. Vaillancourt, D. Talbot, RPN/CMC;
   !        adapted for CMC/RPN physics (May 2006)

   !@Revisions
   ! 001  J. Li (Dec 05,2007) -  new version for gcm15g:  revised data for ri0,ri2.
   ! 002  P. vaillancourt, A-M Leduc  (Apr 2010) - Integrating the  Li/Lazare 2009 version. (raylev2)

   !@Object
   !        Rayleigh scattering for each sub-band in bands1, visible region
   !        taur is the optical depth rayleigh scattering for a given layer
   !        for uvc (35700 - 50000 cm^-1), since the optical depth of o3 and
   !        o2 are very large, rayleigh scattering effect is neglected, it
   !        is shown even for 10% o3 amount of the standard atmo, the
   !        rayleigh scattering for uvc still can be neglected.
   !        For par and uva, since their spectral ranges are very wide, small
   !        errors could occur for large zenith angle, slightly adjustment
   !        is needed, this does mean the rayleigh optical depth is related
   !        solar zenith angle for multiple scattering process in swtran.
   !
   !@Arguments
   ! taur   rayleigh optical depth
   ! dp     air mass path for a model layer (explained in raddriv).
   ! rmu3   a factor of solar zenith angle, given in raddriv

   integer k, i
   real ri0(6), ri2(3)

   data ri0 / .67758e-04, .19425e-03, .52145e-03, .88082e-03, &
        .10541e-02, .12835e-02 /
   data ri2 / .28000e-05, .21300e-04, .36000e-04 /



   if (ig .le. 3) then
      do k = 1, lay
         do i = il1, il2
            taur(i,k) = (ri0(ig) - ri2(ig) * rmu3(i)) * dp(i,k)
         enddo
      enddo
   else
      do k = 1, lay
         do i = il1, il2
            taur(i,k) =  ri0(ig) * dp(i,k)
         enddo
      enddo
   endif

   return
end subroutine ccc2_raylev2
